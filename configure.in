AC_INIT(bonobo/bonobo-ui-engine.h)

AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(libbonoboui, 2.2.3)

AM_MAINTAINER_MODE

dnl
dnl Due to the sed scripts being split on 90 line
dnl blocks, this macro needs to be right at the beggining.
dnl
AC_PROG_INTLTOOL

AC_ISC_POSIX
AC_PROG_CC
AC_STDC_HEADERS
AM_PROG_LIBTOOL
AC_PROG_YACC
AC_PATH_PROGS(PATH_TO_XRDB, "xrdb")

GNOME_COMPILE_WARNINGS

GETTEXT_PACKAGE=libbonoboui-2.0
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE,"$GETTEXT_PACKAGE")
gnomelocaledir='${prefix}/${DATADIRNAME}/locale'
AC_SUBST(gnomelocaledir)

ALL_LINGUAS="am ar az be bg ca cs cy da de el es fa fi fr he hi hu id it ja kn ko lv ml mn ms nl nn no pl pt pt_BR ro ru sk sl sr sr@Latn sv ta tr uk vi wa yi zh_CN zh_TW"
AM_GLIB_GNU_GETTEXT

AC_SUBST(CFLAGS)
AC_SUBST(CPPFLAGS)
AC_SUBST(LDFLAGS)

dnl
dnl Start of pkg-config checks
dnl
BONOBOUI_MODULES="libgnomecanvas-2.0 >= 1.116.0 \
		  libbonobo-2.0 >= 2.0.0 \
		  libgnome-2.0 >= 1.116.0 \
		  bonobo-activation-2.0 >= 1.0.0 \
		  libxml-2.0 >= 2.4.20 \
		  gconf-2.0 >= 1.1.9 \
		  gtk+-2.0 >= 2.2.0"
PKG_CHECK_MODULES(BONOBOUI, $BONOBOUI_MODULES)
AC_SUBST(BONOBOUI_CFLAGS)
AC_SUBST(BONOBOUI_LIBS)

AC_MSG_CHECKING([for gtk+ multihead support])
if $PKG_CONFIG --atleast-version 2.1.0 gtk+-2.0; then
   AC_DEFINE(HAVE_GTK_MULTIHEAD,,[gtk+ with multihead support found])
   AC_MSG_RESULT(yes)
else
   AC_MSG_RESULT(no)
fi

AC_MSG_CHECKING([for broken, old development packages])
if $PKG_CONFIG --exists gdk_pixbuf; then
	if $PKG_CONFIG --modversion 'gdk_pixbuf >= 0.12'; then
		AC_MSG_RESULT([parallel install ok]);
	else
		AC_ERROR([If you have gdk-pixbuf you need at least ver 0.12]);
	fi
else
	AC_MSG_RESULT([clean])
fi;

PKG_CHECK_MODULES(BONOBOUI_GLADE, $BONOBOUI_MODULES libglade-2.0 >= 1.99.11)
AC_SUBST(BONOBOUI_GLADE_CFLAGS)
AC_SUBST(BONOBOUI_GLADE_LIBS)

# glib-genmarshal
AC_PATH_PROG(GLIB_GENMARSHAL, glib-genmarshal)
# glib-mkenums
AC_PATH_PROG(GLIB_MKENUMS, glib-mkenums)

##################################################
# Check for gtk-doc.
##################################################
AC_ARG_WITH(html-dir, [  --with-html-dir=PATH path to installed docs ])

if test "x$with_html_dir" = "x" ; then
  HTML_DIR='${datadir}/gtk-doc/html'
else
  HTML_DIR=$with_html_dir
fi

AC_SUBST(HTML_DIR)

AC_CHECK_PROG(GTKDOC, gtkdoc-mkdb, true, false)

gtk_doc_min_version=0.6
if $GTKDOC ; then 
    gtk_doc_version=`gtkdoc-mkdb --version`
    AC_MSG_CHECKING([gtk-doc version ($gtk_doc_version) >= $gtk_doc_min_version])

    IFS="${IFS= 	}"; bonobo_save_IFS="$IFS"; IFS="."
    set $gtk_doc_version
    for min in $gtk_doc_min_version ; do
        cur=$1; shift
        if test -z $min ; then break; fi
        if test -z $cur ; then GTKDOC=false; break; fi
        if test $cur -gt $min ; then break ; fi
        if test $cur -lt $min ; then GTKDOC=false; break ; fi
    done
    IFS="$bonobo_save_IFS"

    if $GTKDOC ; then
      AC_MSG_RESULT(yes)
    else
      AC_MSG_RESULT(no)
    fi
fi

dnl Let people disable the gtk-doc stuff.
AC_ARG_ENABLE(gtk-doc, [  --enable-gtk-doc  Use gtk-doc to build documentation [default=auto]], enable_gtk_doc="$enableval", enable_gtk_doc=auto)

if test x$enable_gtk_doc = xauto ; then
  if test x$GTKDOC = xtrue ; then
    enable_gtk_doc=yes
  else
    enable_gtk_doc=no 
  fi
fi

AM_CONDITIONAL(ENABLE_GTK_DOC, test x$enable_gtk_doc = xyes)
# gtk-doc check end.


AC_OUTPUT([
Makefile
po/Makefile.in
bonobo/Makefile
bonobo/libbonoboui-2.0.pc
glade/Makefile
samples/Makefile
samples/controls/Makefile
samples/bonoboui/Makefile
samples/compound-doc/Makefile
samples/compound-doc/container/Makefile
tests/Makefile
tests/control/Makefile
doc/Makefile
doc/api/Makefile
doc/api/figures/Makefile
])
