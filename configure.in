AC_INIT(message-of-doom)

AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(libbonoboui, 1.110.2)

AM_MAINTAINER_MODE

dnl
dnl Due to the sed scripts being split on 90 line
dnl blocks, this macro needs to be right at the beggining.
dnl
AC_PROG_INTLTOOL

GNOME_COMMON_INIT
GNOME_PLATFORM_GNOME_2(yes, force)

AC_ISC_POSIX
AC_PROG_CC
AC_STDC_HEADERS
AM_PROG_LIBTOOL
AC_PROG_YACC
AC_PATH_PROGS(PATH_TO_XRDB, "xrdb")

GNOME_COMPILE_WARNINGS

GETTEXT_PACKAGE=libbonoboui-2.0
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE,"$GETTEXT_PACKAGE")

ALL_LINGUAS="az bg ca es fr hu it ja nn no pt ru sk sv ta tr uk wa zh_CN zh_TW"
AM_GLIB_GNU_GETTEXT

AC_SUBST(CFLAGS)
AC_SUBST(CPPFLAGS)
AC_SUBST(LDFLAGS)

dnl
dnl Start of pkg-config checks
dnl
BONOBOUI_MODULES="libgnomecanvas-2.0 >= 1.110.0 libbonobo-2.0 >= 1.110.0 \
		  libgnome-2.0 >= 1.110.0 bonobo-activation-2.0 >= 0.9.3 \
		  gconf-2.0 >= 1.1.7 gtk+-2.0 >= 1.3.12"
PKG_CHECK_MODULES(BONOBOUI, $BONOBOUI_MODULES)
AC_SUBST(BONOBOUI_CFLAGS)
AC_SUBST(BONOBOUI_LIBS)

PKG_CHECK_MODULES(BONOBOUI_GLADE, $BONOBOUI_MODULES libglade-2.0 >= 1.99.5)
AC_SUBST(BONOBOUI_GLADE_CFLAGS)
AC_SUBST(BONOBOUI_GLADE_LIBS)

# glib-genmarshal
AC_PATH_PROG(GLIB_GENMARSHAL, glib-genmarshal)
# glib-mkenums
AC_PATH_PROG(GLIB_MKENUMS, glib-mkenums)

AC_OUTPUT([
Makefile
po/Makefile.in
bonobo/Makefile
bonobo/libbonoboui-2.0.pc
glade/Makefile
samples/Makefile
samples/controls/Makefile
samples/bonoboui/Makefile
samples/compound-doc/Makefile
samples/compound-doc/container/Makefile
tests/Makefile
tests/control/Makefile
doc/Makefile
])

cat $srcdir/message-of-doom
